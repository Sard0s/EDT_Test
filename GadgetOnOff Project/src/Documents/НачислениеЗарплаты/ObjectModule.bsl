
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ДополнительныеНачисления
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаНачисленияДоп Из НачисленияДоп Цикл
		Движение2 = Движения.ДополнительныеНачисления.Добавить();
		Движение2.Сторно = Ложь;
		Движение2.ВидРасчета = ТекСтрокаНачисленияДоп.ВидРасчета;
		Движение2.ПериодРегистрации = НачалоМесяца(Дата);
		
		ГодоваяПремия = ПланыВидовРасчета.ДополнительныеНачисления.ГодоваяПремия;
		Если ТекСтрокаНачисленияДоп.ВидРасчета = ГодоваяПремия Тогда	
			Движение2.БазовыйПериодНачало = НачалоГода(Дата);
			Движение2.БазовыйПериодКонец = КонецГода(Дата);
		Иначе
			Движение2.БазовыйПериодНачало = НачалоМесяца(Дата);
			Движение2.БазовыйПериодКонец = КонецМесяца(Дата);
		КонецЕсли;
		
		Движение2.Сотрудник = ТекСтрокаНачисленияДоп.Сотрудник;
		Движение2.Размер = ТекСтрокаНачисленияДоп.Размер
		//Движение.Результат = 0;
	КонецЦикла;

	Движения.ДополнительныеНачисления.Записать();
	
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаНачисления Из Начисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = ТекСтрокаНачисления.ДатаОкончания;
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = ТекСтрокаНачисления.Сотрудник;
		Движение.График = ТекСтрокаНачисления.График;
		//Движение.Результат = 0;
		//Движение.Факт = 0;
		//Движение.Размер = ТекСтрокаНачисления.Размер;
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();
	
	
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	//Расчет ОКЛАДА
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеЧасыФактическийПериодДействия, 0) КАК ФактЧасы,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеЧасыПериодДействия, 0) КАК ПланЧасы,
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОкладыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Ссылка
		|				И ВидРасчета = &Оклад) КАК ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОкладыСотрудников.СрезПоследних(&Дата, ) КАК ОкладыСотрудниковСрезПоследних
		|		ПО ОсновныеНачисленияДанныеГрафика.Сотрудник = ОкладыСотрудниковСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", НачалоМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого СтрДвижение Из Движения.ОсновныеНачисления Цикл
		Если СтрДвижение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда	
			Продолжить;	
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		ВыборкаДетальныеЗаписи.НайтиСледующий(СтрДвижение.НомерСтроки,"НомерСтроки");
		СтрДвижение.Факт = ВыборкаДетальныеЗаписи.ФактЧасы;
		
		Если ВыборкаДетальныеЗаписи.Оклад = 0 Тогда
			Сообщить("Не установен оклад " + ВыборкаДетальныеЗаписи.Сотрудник);
			Отказ = Истина;
		Иначе	
			СтрДвижение.Результат = ВыборкаДетальныеЗаписи.ФактЧасы / ВыборкаДетальныеЗаписи.ПланЧасы * ВыборкаДетальныеЗаписи.Оклад;
		КонецЕсли;
		СтрДвижение.Размер = ВыборкаДетальныеЗаписи.Оклад; 
		
	КонецЦикла;
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	//Расчет БОЛЬНИЧНОГО
	
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ДниБолезни
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Ссылка
		|				И ВидРасчета = &Больничный) КАК ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("Больничный", ПланыВидовРасчета.ОсновныеНачисления.Больничный);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтавкаПоБольничному = Константы.СтавкаПоБольничномуВДень.Получить();
	
	Для Каждого СтрДвижение Из Движения.ОсновныеНачисления Цикл
		Если СтрДвижение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда	
			Продолжить;	
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		ВыборкаДетальныеЗаписи.НайтиСледующий(СтрДвижение.НомерСтроки,"НомерСтроки");
		
		СтрДвижение.Результат = ВыборкаДетальныеЗаписи.ДниБолезни * СтавкаПоБольничному;		
		СтрДвижение.Факт = ВыборкаДетальныеЗаписи.ДниБолезни;
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	//Расчет КОМАНДИРОВКИ
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ДниКомандировки
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Ссылка
		|				И ВидРасчета = &Командировка) КАК ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("Командировка", ПланыВидовРасчета.ОсновныеНачисления.Командировка);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтавкаПоКомандировке = Константы.СтавкаПоКомандировкеВДень.Получить();
	
	Для Каждого СтрДвижение Из Движения.ОсновныеНачисления Цикл
		Если СтрДвижение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Командировка Тогда	
			Продолжить;	
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		ВыборкаДетальныеЗаписи.НайтиСледующий(СтрДвижение.НомерСтроки,"НомерСтроки");
		
		СтрДвижение.Результат = ВыборкаДетальныеЗаписи.ДниКомандировки * СтавкаПоКомандировке;		
		СтрДвижение.Факт = ВыборкаДетальныеЗаписи.ДниКомандировки;
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	//Расчет ОТПУСКА
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеДниФактическийПериодДействия, 0) КАК ДниОтпуска
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Ссылка
		|				И ВидРасчета = &Отпуск) КАК ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого СтрДвижение Из Движения.ОсновныеНачисления Цикл
		Если СтрДвижение.ВидРасчета <> ПланыВидовРасчета.ОсновныеНачисления.Отпуск Тогда	
			Продолжить;	
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		ВыборкаДетальныеЗаписи.НайтиСледующий(СтрДвижение.НомерСтроки,"НомерСтроки");
		
		СтрДвижение.Результат = СтрДвижение.Размер;		
		СтрДвижение.Факт = ВыборкаДетальныеЗаписи.ДниОтпуска;
		
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	Движения.ОсновныеНачисления.Записать(,Истина);
	
	//Расчет ПРЕМИИ
	
	 	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
		|			&МассивИзмерений,
		|			&МассивИзмерений,
		|			,
		|			ВидРасчета = &Премия
		|				И Регистратор = &Ссылка) КАК ДополнительныеНачисленияБазаОсновныеНачисления";
	
	МассивИзмерений = Новый Массив;
	МассивИзмерений.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("МассивИзмерений", МассивИзмерений);
	Запрос.УстановитьПараметр("Премия", ПланыВидовРасчета.ДополнительныеНачисления.Премия);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	Для Каждого СтрДвижение Из Движения.ДополнительныеНачисления Цикл
		
		Если СтрДвижение.ВидРасчета <> ПланыВидовРасчета.ДополнительныеНачисления.Премия Тогда	
			Продолжить;	
		КонецЕсли;	
		ВыборкаДетальныеЗаписи.Сбросить();
		ВыборкаДетальныеЗаписи.НайтиСледующий(СтрДвижение.НомерСтроки,"НомерСтроки");	
		СтрДвижение.Результат = СтрДвижение.Размер / 100 * ВыборкаДетальныеЗаписи.РезультатБаза;	
	КонецЦикла;
	
	//Расчет НАДБАВКИ
	
	 	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисления.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_ДанныеРегистра
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.Регистратор = &Ссылка
		|	И ДополнительныеНачисления.ВидРасчета = &Надбавка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеРегистра.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ПродажиОбороты.СтоимостьОборот, 0) КАК СтоимостьОборот,
		|	ПродажиОбороты.ОтветственныйСотрудник КАК ОтветственныйСотрудник
		|ИЗ
		|	ВТ_ДанныеРегистра КАК ВТ_ДанныеРегистра
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи.Обороты(
		|				&ДатаНачала,
		|				&ДатаОкончания,
		|				,
		|				ОтветственныйСотрудник В
		|					(ВЫБРАТЬ
		|						ВТ_ДанныеРегистра.Сотрудник КАК Сотрудник
		|					ИЗ
		|						ВТ_ДанныеРегистра КАК ВТ_ДанныеРегистра)) КАК ПродажиОбороты
		|		ПО ВТ_ДанныеРегистра.Сотрудник = ПродажиОбороты.ОтветственныйСотрудник";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДобавитьМесяц(НачалоМесяца(Дата),-1));
	Запрос.УстановитьПараметр("ДатаОкончания", ДобавитьМесяц(КонецМесяца(Дата),-1));
	Запрос.УстановитьПараметр("Надбавка", ПланыВидовРасчета.ДополнительныеНачисления.Надбавка);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	Для Каждого СтрДвижение Из Движения.ДополнительныеНачисления Цикл
		
		Если СтрДвижение.ВидРасчета <> ПланыВидовРасчета.ДополнительныеНачисления.Надбавка Тогда	
			Продолжить;	
		КонецЕсли;	
		ВыборкаДетальныеЗаписи.Сбросить();
		ВыборкаДетальныеЗаписи.НайтиСледующий(СтрДвижение.НомерСтроки,"НомерСтроки");	
		СтрДвижение.Результат = СтрДвижение.Размер / 100 * ВыборкаДетальныеЗаписи.СтоимостьОборот;	
	КонецЦикла;
	
	//Расчет ГОДОВОЙ ПРЕМИИ
	
	 	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
		|			&МассивИзмерений,
		|			&МассивИзмерений,
		|			,
		|			ВидРасчета = &ГодоваяПремия
		|				И Регистратор = &Ссылка) КАК ДополнительныеНачисленияБазаОсновныеНачисления";
	
	МассивИзмерений = Новый Массив;
	МассивИзмерений.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("МассивИзмерений", МассивИзмерений);
	Запрос.УстановитьПараметр("ГодоваяПремия", ПланыВидовРасчета.ДополнительныеНачисления.ГодоваяПремия);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	Для Каждого СтрДвижение Из Движения.ДополнительныеНачисления Цикл
		
		Если СтрДвижение.ВидРасчета <> ПланыВидовРасчета.ДополнительныеНачисления.ГодоваяПремия Тогда	
			Продолжить;	
		КонецЕсли;	
		ВыборкаДетальныеЗаписи.Сбросить();
		ВыборкаДетальныеЗаписи.НайтиСледующий(СтрДвижение.НомерСтроки,"НомерСтроки");	
		СтрДвижение.Результат = СтрДвижение.Размер / 100 * ВыборкаДетальныеЗаписи.РезультатБаза;	
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать(,Истина);

КонецПроцедуры